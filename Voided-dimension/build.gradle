plugins {
    id 'fabric-loom' version '1.10-SNAPSHOT'
    id 'java'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    mavenCentral()
    maven {
        name = 'Fabric'
        url = 'https://maven.fabricmc.net/'
    }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
    }
    maven {
        name = 'GeckoLib'
        url = 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
    }

}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation "maven.modrinth:worldgen-devtools:1.1.0+1.21.1"
    modImplementation "maven.modrinth:carpet:1.4.147"  // Nueva versiÃ³n de Carpet

    // GeckoLib para Fabric 1.21.1
    modImplementation "software.bernie.geckolib:geckolib-fabric-1.21.1:4.7.3"
}


processResources {
    inputs.property "version", project.version
    // Prefer src/main/resources over src/generated when the same path exists
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

// Evitar fallos por duplicados en archivos de salida (incluido sourcesJar)
tasks.withType(AbstractArchiveTask).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

loom {
    runs {
        datagen {
            client()
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/generated") }"
            vmArg "-Dfabric-api.datagen.modid=voideddimension"
        }

        clientFresh {
            client()
            name "Client (Fresh World)"
            runDir "run_fresh"
        }

        clientAuto {
            client()
            name "Client (Auto World)"
            runDir "run_fresh"
            programArg "--quickPlaySingleplayer"
            programArg "AutoTest"
        }
    }
}

// Borra mundos del run dir fresco antes de correr el cliente
tasks.register('cleanFreshRunDir') {
    doLast {
        def dir = file('run_fresh/saves')
        if (dir.exists()) {
            dir.listFiles()?.each { f ->
                if (f.isDirectory()) {
                    f.deleteDir()
                } else {
                    f.delete()
                }
            }
        }
    }
}

tasks.register('freshRunClient') {
    dependsOn 'cleanFreshRunDir'
    dependsOn 'runClientFresh'
}

tasks.register('autoTestClient') {
    dependsOn 'runClientAuto'
}

sourceSets {
    main {
        resources {
            srcDirs += [
                    './src/generated'
            ]
        }
    }
}